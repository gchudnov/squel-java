apply plugin: "maven"
apply plugin: "signing"
apply plugin: "com.jfrog.bintray"

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

ext."signing.keyId" = localProperties.getProperty("SIGNING_KEY_ID")
ext."signing.password" = localProperties.getProperty("SIGNING_PASSWORD")
ext."signing.secretKeyRingFile" = localProperties.getProperty("SIGNING_SECRET_KEY_RING_FILE")

task sourceJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}


install {
    repositories.mavenInstaller {
        pom.project {
            name POM_NAME
            artifactId POM_ARTIFACT_ID
            packaging POM_PACKAGING
            description POM_DESCRIPTION
            url PKG_WEBSITE_URL

            scm {
                url PKG_VCS_URL
                connection 'scm:git:' + PKG_VCS_URL
                developerConnection 'scm:git:' + PKG_VCS_URL
            }

            licenses {
                license {
                    name 'MIT'
                    url 'https://github.com/gchudnov/bspec/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'gchudnov'
                    name 'Grigoriy Chudnov'
                    email 'g.chudnov@gmail.com'
                }
            }
        }

        signing.sign new java.io.File(getMavenPomDir(), "/pom-default.xml")
    }

//    repositories.mavenDeployer {
//        repository(url: "file://localhost/tmp/squel/")
//        beforeDeployment { MavenDeployment deployment ->
//            signing.signPom(deployment)
//        }
//    }
}


version = VERSION_NAME
group = GROUP


signing {
    sign configurations.archives
}

bintray {
    user = localProperties.getProperty("BINTRAY_USER")
    key = localProperties.getProperty("BINTRAY_APIKEY")

    dryRun = false

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = PKG_NAME
        desc = PKG_DESC
        websiteUrl = PKG_WEBSITE_URL
        issueTrackerUrl = PKG_ISSUE_TRACKER_URL
        vcsUrl = PKG_VCS_URL
        licenses = ["MIT"]
        labels = ['SQL', 'builder']
        publicDownloadNumbers = true
        version {
            name = VERSION_NAME
            vcsTag = 'v' + VERSION_NAME
        }
    }
}
